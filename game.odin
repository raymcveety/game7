package main

import "base:runtime"
import "core:c"
import "core:fmt"
import "core:log"
import "core:math"
import "core:math/linalg"
import "core:math/linalg/hlsl"
import "core:math/noise"
import "core:mem"
import "core:os"
import "core:path/filepath"
import "core:slice"
import "core:strings"
import "core:time"

import "vendor:sdl2"
import vk "vendor:vulkan"

import vkw "desktop_vulkan_wrapper"
import imgui "odin-imgui"

DEFAULT_COMPONENT_MAP_CAPACITY :: 1024

GRAVITY_ACCELERATION : hlsl.float3 : {0.0, 0.0, 2.0 * -9.8}           // m/s^2
TERMINAL_VELOCITY :: -100000.0                                  // m/s
ENEMY_THROW_SPEED :: 15.0
DEFAULT_FACING_DIRECTION :: hlsl.float3 {1.0, 0.0, 0.0}

MAX_SPLITSCREEN_PLAYERS :: 4

Transform :: struct {
    position: hlsl.float3,
    rotation: quaternion128,
    scale: f32,
}
get_transform_matrix :: proc(tform: Transform, scale: f32 = 1.0) -> hlsl.float4x4 {
    return translation_rotation_scaling_matrix(tform.position, tform.rotation, scale * tform.scale)
}

TransformDelta :: struct {
    velocity: hlsl.float3,
    rotational_velocity: quaternion128,
}

tick_transform_deltas :: proc(game_state: ^GameState, dt: f32) {
    scoped_event(&profiler, "tick_transform_deltas")
    for id, &delta in game_state.transform_deltas {
        tform := &game_state.transforms[id]
        tform.position += dt * delta.velocity
    }
}

tick_looping_animations :: proc(game_state: ^GameState, renderer: Renderer, dt: f32) {
    scoped_event(&profiler, "tick_looping_animations")
    for id in game_state.looping_animations {
        instance := &game_state.skinned_models[id]
        instance.anim_t = instance.anim_t + dt
        duration := get_animation_duration(renderer, instance.anim_idx)
        for instance.anim_t > duration {
            instance.anim_t -= duration
        }
    }
}

tick_coins :: proc(game_state: ^GameState, audio_system: ^AudioSystem) {    
    scoped_event(&profiler, "tick_coins")
    rot := z_rotate_quaternion(game_state.time)
    z_offset := 0.25 * math.sin(game_state.time)

    for id in game_state.coins {
        model := &game_state.static_models[id]
        model.pos_offset.z = z_offset

        tform := &game_state.transforms[id]
        tform.rotation = rot
    }
}

new_coin :: proc(game_state: ^GameState, position: hlsl.float3) -> EntityID {
    id := gamestate_next_id(game_state)

    game_state.transforms[id] = Transform {
        position = position,
        rotation = linalg.QUATERNIONF32_IDENTITY,
        scale = 0.6
    }
    game_state.static_models[id] = StaticModelInstance {
        handle = game_state.coin_mesh,
        flags = {}
    }
    append(&game_state.coins, id)

    return id
}

delete_coin :: proc(game_state: ^GameState, idx: int) {
    id := game_state.coins[idx]
    delete_key(&game_state.transforms, id)
    delete_key(&game_state.static_models, id)
    unordered_remove(&game_state.coins, idx)
}

EnemyAI :: struct {
    home_position: hlsl.float3,
    facing: hlsl.float3,
    visualize_home: bool,
    state: EnemyState,
    init_state: EnemyState,
    init_pos: hlsl.float3,
    timer_start: time.Time
}
default_enemyai :: proc(game_state: GameState) -> EnemyAI {
    return {
        home_position = {},
        init_pos = {},
        facing = {0.0, 1.0, 0.0},
        visualize_home = false,
        state = .Wandering,
        timer_start = time.now()
    }
}

new_enemy :: proc(game_state: ^GameState, position: hlsl.float3, scale: f32, state: EnemyState) -> EntityID {
    id := gamestate_next_id(game_state)
    game_state.transforms[id] = Transform {
        position = position,
        rotation = linalg.QUATERNIONF32_IDENTITY,
        scale = scale
    }
    ai := default_enemyai(game_state^)
    ai.state = state
    ai.init_state = state
    ai.home_position = position
    ai.init_pos = position
    game_state.enemy_ais[id] = ai

    game_state.spherical_bodies[id] = SphericalBody {
        radius = scale,
        gravity_scale = 1.0,
        state = .Falling
    }

    game_state.static_models[id] = StaticModelInstance {
        handle = game_state.enemy_mesh,
        flags = {}
    }

    return id
}

delete_enemy :: proc(game_state: ^GameState, id: EntityID) {
    delete_key(&game_state.transforms, id)
    delete_key(&game_state.spherical_bodies, id)
    delete_key(&game_state.enemy_ais, id)
    delete_key(&game_state.hovering_enemies, id)
    delete_key(&game_state.thrown_enemy_ais, id)
    delete_key(&game_state.static_models, id)
}

check_characters_grabbing :: proc(
    game_state: ^GameState,
    enemy_id: EntityID,
    respawn_pos: hlsl.float3,
    enemy_sphere: Sphere,
    e_type: EnemyType
) -> bool {
    // Check if overlapping player grab
    for pid, &char in game_state.character_controllers {
        enemy_to_remove: Maybe(EntityID)
        if char.vortex_t < char.bullet_travel_time {
            // char is currently using move
            ptform := game_state.transforms[pid]
            vsphere := Sphere {
                position = ptform.position,
                radius = VORTEX_MAX_RADIUS
            }
            if are_spheres_overlapping(vsphere, enemy_sphere) {
                // Got grabbed
                char.enemy_respawn_pos = respawn_pos
                enemy_to_remove = enemy_id
            }
        }

        enemy_remove_id, ok := enemy_to_remove.?
        if ok {
            char.vortex_t = char.bullet_travel_time
            char.flags += {.HoldingEnemy}
            char.enemy_type = e_type
            return true
        }
    }
    return false
}

ENEMY_HOME_RADIUS :: 4.0
ENEMY_LUNGE_SPEED :: 20.0
ENEMY_JUMP_SPEED :: 6.0                 // m/s
tick_enemy_ai :: proc(game_state: ^GameState, audio_system: ^AudioSystem, dt: f32) {
    scoped_event(&profiler, "tick_enemy_ai")
    for id, &enemy in game_state.enemy_ais {
        transform := &game_state.transforms[id]

        body := &game_state.spherical_bodies[id]
        switch enemy.state {
            case .BrainDead: {
                body.velocity.xy = {}
            }
            case .Wandering: {
                sample_point := [2]f64 {f64(game_state.time), f64(id)}
                t := 5.0 * dt * noise.noise_2d(game_state.rng_seed, sample_point)
                rotq := z_rotate_quaternion(t)
                enemy.facing = linalg.quaternion128_mul_vector3(rotq, enemy.facing)

                body.velocity.xy = hlsl.normalize(enemy.facing.xy)

                // Check if we have to charge at any players
                for player_id in game_state.local_players {
                    char_tform := &game_state.transforms[player_id]
                    char_col := &game_state.spherical_bodies[player_id]
                    if char_col.state != .Grounded {
                        continue
                    }
                    dist_to_player := hlsl.distance(char_tform.position, transform.position)
                    if dist_to_player < ENEMY_HOME_RADIUS {
                        enemy.facing = char_tform.position - transform.position
                        enemy.facing.z = 0.0
                        enemy.facing = hlsl.normalize(enemy.facing)
                        body.velocity = {0.0, 0.0, ENEMY_JUMP_SPEED}
                        enemy.state = .AlertedBounce
                        body.state = .Falling
                        enemy.timer_start = time.now()
                        enemy.home_position = transform.position
                        play_sound_effect(audio_system, game_state.jump_sound)
                    }
                }

                if time.diff(enemy.timer_start, time.now()) > time.Duration(5.0 * SECONDS_TO_NANOSECONDS) {
                    // Start resting
                    enemy.timer_start = time.now()
                    enemy.state = .Resting
                    body.velocity = {}
                }
            }
            case .AlertedBounce: {
                if body.state == .Grounded {
                    enemy.state = .AlertedCharge
                    body.velocity.xy += enemy.facing.xy * ENEMY_LUNGE_SPEED
                    body.velocity.z = ENEMY_JUMP_SPEED / 2.0
                    body.state = .Falling
                    play_sound_effect(audio_system, game_state.jump_sound)
                }
            }
            case .AlertedCharge: {
                enemy.home_position = transform.position
                if body.state == .Grounded {
                    body := game_state.spherical_bodies[id]
                    enemy.state = .Resting
                    enemy.timer_start = time.now()
                    body.velocity = {}
                }
            }
            case .Resting: {
                if time.diff(enemy.timer_start, time.now()) > time.Duration(0.75 * SECONDS_TO_NANOSECONDS) {
                    // Start wandering
                    enemy.timer_start = time.now()
                    enemy.state = .Wandering
                }
            }
        }

        // Restrict enemy movement based on home position
        {
            disp := transform.position - enemy.home_position
            l := hlsl.length(disp.xy)
            if l > ENEMY_HOME_RADIUS {
                transform.position.xy += (l - ENEMY_HOME_RADIUS) * hlsl.normalize((enemy.home_position - transform.position).xy)
            }
        }

        // Check for collision with player
        for player_id in game_state.local_players {
            player_tform := &game_state.transforms[player_id]
            player_collision := &game_state.spherical_bodies[player_id]

            ps := Sphere {
                position = player_tform.position,
                radius = player_collision.radius
            }
            es := Sphere {
                position = transform.position,
                radius = body.radius
            }
            if are_spheres_overlapping(ps, es) {
                append(&game_state.character_hit_events, HitEvent {
                    player_id = player_id
                })
            }
        }

        esphere := Sphere {
            position = transform.position,
            radius = body.radius
        }
        if check_characters_grabbing(game_state, id, enemy.init_pos, esphere, .Regular) {
            delete_enemy(game_state, id)
        }

        // Get transform rotation quaternion from facing direction
        {
            transform.rotation = linalg.quaternion_between_two_vector3_f32(hlsl.float3{0.0, 1.0, 0.0}, enemy.facing)
        }
    }
}

HoveringEnemy :: struct {
    home_position: hlsl.float3,
    radius: f32,
}

new_hovering_enemy :: proc(game_state: ^GameState, position: hlsl.float3, scale: f32) -> EntityID {
    id := gamestate_next_id(game_state)
    game_state.transforms[id] = Transform {
        position = position,
        rotation = linalg.QUATERNIONF32_IDENTITY,
        scale = scale
    }
    game_state.hovering_enemies[id] = HoveringEnemy {
        home_position = position,
        radius = scale,
    }
    game_state.static_models[id] = StaticModelInstance {
        handle = game_state.enemy_mesh,
        flags = {}
    }

    return id
}

delete_hovering_enemy :: proc(game_state: ^GameState, id: EntityID) {
    delete_key(&game_state.transforms, id)
    delete_key(&game_state.hovering_enemies, id)
    delete_key(&game_state.static_models, id)
}

tick_hovering_enemies :: proc(game_state: ^GameState, dt: f32) {
    scoped_event(&profiler, "tick_hovering_enemies")
    for id, enemy in game_state.hovering_enemies {
        transform := &game_state.transforms[id]
        offset := hlsl.float3 {0, 0, 1.5 * math.sin(game_state.time)}
        transform.position = enemy.home_position + offset

        esphere := Sphere {
            position = transform.position,
            radius = enemy.radius
        }
        if check_characters_grabbing(game_state, id, enemy.home_position, esphere, .Hovering) {
            delete_hovering_enemy(game_state, id)
            continue
        }

        // Check for collision with player
        for player_id in game_state.local_players {
            player_tform := &game_state.transforms[player_id]
            player_collision := &game_state.spherical_bodies[player_id]

            ps := Sphere {
                position = player_tform.position,
                radius = player_collision.radius
            }
            es := Sphere {
                position = transform.position,
                radius = enemy.radius
            }
            if are_spheres_overlapping(ps, es) {
                append(&game_state.character_hit_events, HitEvent {})
            }
        }
    }
}

ThrownEnemyAI :: struct {
    respawn_position: hlsl.float3,
    radius: f32,
    state: EnemyState,
    e_type: EnemyType,
}

tick_thrown_enemies :: proc(game_state: ^GameState) {
    scoped_event(&profiler, "tick_thrown_enemies")
    to_remove: Maybe(EntityID)
    for id, enemy in game_state.thrown_enemy_ais {
        transform := &game_state.transforms[id]
        closest_pt := closest_pt_terrain(transform.position, game_state.triangle_meshes)

        if hlsl.distance(closest_pt, transform.position) < enemy.radius {
            to_remove = id

            // Respawn enemy
            switch enemy.e_type {
                case .Regular: {
                    new_enemy(game_state, enemy.respawn_position, transform.scale * 5/4, enemy.state)
                }
                case .Hovering: {
                    new_hovering_enemy(game_state, enemy.respawn_position, transform.scale * 5/4)
                }
            }
        }
    }

    remove_id, remove := to_remove.?
    if remove {
        delete_thrown_enemy(game_state, remove_id)
    }
}

new_thrown_enemy :: proc(
    game_state: ^GameState,
    position: hlsl.float3,
    velocity: hlsl.float3,
    state: EnemyState,
    respawn_position: hlsl.float3,
    e_type: EnemyType,
) -> EntityID {
    id := gamestate_next_id(game_state)
    game_state.transforms[id] = Transform {
        position = position,
        scale = 0.5 * 0.8
    }
    game_state.transform_deltas[id] = TransformDelta {
        velocity = velocity
    }
    game_state.static_models[id] = StaticModelInstance {
        handle = game_state.enemy_mesh,
        flags = {.Glowing}
    }
    game_state.thrown_enemy_ais[id] = ThrownEnemyAI {
        respawn_position = respawn_position,
        radius = 0.5 * 0.8,
        state = state,
        e_type = e_type
    }

    return id
}

delete_thrown_enemy :: proc(game_state: ^GameState, id: EntityID) {
    delete_key(&game_state.transforms, id)
    delete_key(&game_state.transform_deltas, id)
    delete_key(&game_state.static_models, id)
    delete_key(&game_state.thrown_enemy_ais, id)
}

SphericalBody :: struct {
    velocity: hlsl.float3,
    radius: f32,
    gravity_scale: f32,
    state: CollisionState,
}

tick_spherical_bodies :: proc(game_state: ^GameState, dt: f32) {
    scoped_event(&profiler, "tick_spherical_bodies")
    for id, &body in game_state.spherical_bodies {
        scoped_event(&profiler, "tick_spherical_bodies iteration")
        transform := &game_state.transforms[id]

        simple_continuous_collision_detection :: proc(
            motion_interval: LineSegment,
            transform: ^Transform,
            radius: f32,
            terrain: map[EntityID]TriangleMesh
        ) -> (collision_normal: hlsl.float3, id: EntityID, ok: bool) {
            segment_collision_t, segment_collision_normal, collided_with_id, segment_intersected := intersect_segment_terrain_with_normal_and_id(motion_interval, terrain)
            if segment_intersected {
                segment_collision := sample_segment(motion_interval, segment_collision_t)
                transform.position = segment_collision + segment_collision_normal * radius
                collision_normal = segment_collision_normal
                ok = true
                id = collided_with_id
            } else {
                closest_pt, closest_pt_normal := closest_pt_terrain_with_normal(motion_interval.end, terrain)
                d := hlsl.distance(motion_interval.end, closest_pt)
                if d < radius {
                    // Hit terrain
                    remaining_d := radius - d
                    transform.position = motion_interval.end + remaining_d * closest_pt_normal
                    collision_normal = closest_pt_normal
                    ok = true
                }
            }
            return
        }

        // Body's desired motion interval
        motion_interval := LineSegment {
            start = transform.position,
            end = transform.position + dt * body.velocity
        }

        collided_this_frame := false
        switch body.state {
            case .Grounded: {
                closest_pt, closest_pt_normal: hlsl.float3

                // Compute closest_pt and closest_pt_normal and do preliminary transform update
                segment_collision_t, segment_collision_normal, segment_intersected :=
                    intersect_segment_terrain_with_normal(motion_interval, game_state.triangle_meshes)

                if segment_intersected {
                    transform.position = sample_segment(motion_interval, segment_collision_t)
                    closest_pt = transform.position
                    closest_pt_normal = segment_collision_normal
                } else {
                    transform.position = motion_interval.end
                    closest_pt, closest_pt_normal = closest_pt_terrain_with_normal(transform.position, game_state.triangle_meshes)
                }

                // Check for walking into walls
                if hlsl.distance(closest_pt, transform.position) < body.radius {
                    collided_this_frame = true
                    if hlsl.dot(closest_pt_normal, hlsl.float3{0.0, 0.0, 1.0}) < 0.5 {
                        direction: hlsl.float3
                        direction.xy = hlsl.normalize(closest_pt_normal.xy)

                        // Solve "dot(((r_start + r_dir * t) - closest_pt), closest_pt_normal) = r"
                        // for t and compute it
                        t := (body.radius + hlsl.dot(closest_pt, closest_pt_normal) - hlsl.dot(transform.position, closest_pt_normal)) /
                        //   ---------------------------------------------------------------------------------------------------------
                                                              hlsl.dot(direction, closest_pt_normal)

                        // Use computed t value to move position along direction by the appropriate amount
                        if t > 0.0 {
                            transform.position += direction * t
                        }
                    }
                }

                // Check if we need to bump ourselves up or down
                {
                    tolerance_segment := LineSegment {
                        start = transform.position + {0.0, 0.0, 0.0},
                        end = transform.position + {0.0, 0.0, -body.radius - 0.1}
                    }
                    tolerance_t, normal, id_collided_with, okt := intersect_segment_terrain_with_normal_and_id(tolerance_segment, game_state.triangle_meshes)
                    if okt {
                        // The test line segment intersected with the ground

                        if hlsl.dot(normal, hlsl.float3{0.0, 0.0, 1.0}) >= 0.5 {
                            tolerance_point := sample_segment(tolerance_segment, tolerance_t)
                            transform.position = tolerance_point + {0.0, 0.0, body.radius}
                            body.velocity.z = 0.0
                            body.state = .Grounded
                            game_state.parents[id] = id_collided_with
                        } else {
                            // Floor too steep
                            body.state = .Falling
                        }
                    } else {
                        body.state = .Falling
                    }
                }
            }
            case .Falling: {
                // Update velocity
                body.velocity += dt * body.gravity_scale * GRAVITY_ACCELERATION
                if body.velocity.z < TERMINAL_VELOCITY {
                    body.velocity.z = TERMINAL_VELOCITY
                }

                collision_normal, collided_with_id, ok := simple_continuous_collision_detection(motion_interval, transform, body.radius, game_state.triangle_meshes)
                if ok {
                    collided_this_frame = true
                    n_dot := hlsl.dot(collision_normal, hlsl.float3{0.0, 0.0, 1.0})
                    if n_dot >= 0.5 {
                        // Floor
                        body.velocity.z = 0.0
                        body.state = .Grounded
                        game_state.parents[id] = collided_with_id
                    } else if n_dot < -0.1 {
                        // Ceiling
                        body.velocity.z = 0.0
                    } else {
                        // Wall

                    }
                } else {
                    // Free fallin'
                    transform.position += dt * body.velocity
                }
            }
        }
    }
}

EnemyType :: enum {
    Regular,
    Hovering,
}
CharacterFlag :: enum {
    MovingLeft,
    MovingRight,
    MovingBack,
    MovingForward,
    AlreadyJumped,
    Sprinting,
    HoldingEnemy
}
CharacterFlags :: bit_set[CharacterFlag]
CHARACTER_MAX_HEALTH :: 3
CHARACTER_INVULNERABILITY_DURATION :: 0.5
VORTEX_MAX_RADIUS :: 1.0
CHARACTER_NORMAL_GRAVITY :: 1.0
CHARACTER_HEAVY_GRAVITY :: 2.2
CharacterController :: struct {
    acceleration: hlsl.float3,
    deceleration_speed: f32,
    move_speed: f32,
    sprint_speed: f32,
    jump_speed: f32,
    bullet_travel_time: f32,
    health: u32,
    vortex_t: f32,
    anim_speed: f32,
    time_last_damaged: time.Time,
    flags: CharacterFlags,
    enemy_respawn_pos: hlsl.float3,
    enemy_type: EnemyType,
}

new_local_player_character :: proc(game_state: ^GameState, renderer: ^Renderer, user_config: UserConfiguration, allocator := context.allocator) -> EntityID {
    id := gamestate_next_id(game_state)

    game_state.transforms[id] = Transform {
        position = game_state.level_start,
        scale = 1.0,
    }
    game_state.spherical_bodies[id] = SphericalBody {
        velocity = {},
        radius = 0.6,
        gravity_scale = CHARACTER_HEAVY_GRAVITY,
        state = .Falling
    }
    game_state.character_controllers[id] = CharacterController {
        acceleration = {},
        deceleration_speed = 0.1,
        move_speed = 7.0,
        sprint_speed = 10.0,
        jump_speed = 10.0,
        bullet_travel_time = 0.144,
        health = CHARACTER_MAX_HEALTH,
        vortex_t = 0.144 + 1.0,
        anim_speed = 0.856,
        time_last_damaged = {},
        flags = {}
    }
    cam_id := new_viewport_camera(game_state, id, user_config)
    append(&game_state.viewport_cameras, cam_id)

    // Load animated test glTF model
    path : cstring = "data/models/CesiumMan.glb"
    skinned_model := load_gltf_skinned_model(renderer, path, allocator)
    game_state.skinned_models[id] = SkinnedModelInstance {
        handle = skinned_model,
        pos_offset = {0.0, 0.0, -0.6},
        flags = {}
    }

    return id
}

new_viewport_camera :: proc(game_state: ^GameState, player_id: EntityID, user_config: UserConfiguration) -> EntityID {
    id := gamestate_next_id(game_state)
    game_state.transforms[id] = Transform {
        position = {
            f32(user_config.floats[.FreecamX]),
            f32(user_config.floats[.FreecamY]),
            f32(user_config.floats[.FreecamZ])
        }
    }
    game_state.cameras[id] = FreecamController {
        fov_radians = f32(user_config.floats[.CameraFOV]),
        nearplane = 0.1 / math.sqrt_f32(2.0),
        farplane = 1_000_000.0,
        yaw = f32(user_config.floats[.FreecamYaw]),
        pitch = f32(user_config.floats[.FreecamPitch]),
    }
    if user_config.flags[.FollowCam] {
        game_state.lookat_controllers[id] = LookatController {
            target = player_id,
            vertical_offset = 1.2,
            distance = DEFAULT_LOOKAT_DISTANCE
        }
    }
    return id
}

tick_character_controllers :: proc(game_state: ^GameState, renderer: ^Renderer, all_output_verbs: OutputVerbs, audio_system: ^AudioSystem, dt: f32) {
    scoped_event(&profiler, "tick_character_controllers")
    for id, &char in game_state.character_controllers {

        // Figure out which local player this is
        local_player_idx := 0
        for local_player_idx < MAX_SPLITSCREEN_PLAYERS {
            if id == game_state.local_players[local_player_idx] {
                break
            }
            local_player_idx += 1
        }

        tform := &game_state.transforms[id]
        collision := &game_state.spherical_bodies[id]
        model := &game_state.skinned_models[id]
        camera := &game_state.cameras[game_state.viewport_cameras[local_player_idx]]
        output_verbs := all_output_verbs.recipient_verbs[local_player_idx]

        // Set current xy velocity (and character facing) to whatever user input is
        {
            // X and Z bc view space is x-right, y-up, z-back
            translate_vector := output_verbs.float2s[.PlayerTranslate]
            translate_vector_x := translate_vector.x
            translate_vector_z := translate_vector.y

            // Boolean (keyboard) input handling
            {
                set_character_flags_from_verb :: proc(flags: ^CharacterFlags, d: map[VerbType]bool, verb: VerbType, action: CharacterFlag) {
                    r, ok := d[verb]
                    if ok {
                        if r {
                            flags^ += {action}
                        } else {
                            flags^ -= {action}
                        }
                    }
                }

                flags := &char.flags
                set_character_flags_from_verb(flags, output_verbs.bools, .PlayerTranslateLeft, .MovingLeft)
                set_character_flags_from_verb(flags, output_verbs.bools, .PlayerTranslateRight, .MovingRight)
                set_character_flags_from_verb(flags, output_verbs.bools, .PlayerTranslateBack, .MovingBack)
                set_character_flags_from_verb(flags, output_verbs.bools, .PlayerTranslateForward, .MovingForward)

                if .MovingLeft in flags^ {
                    translate_vector_x += -1.0
                }
                if .MovingRight in flags^ {
                    translate_vector_x += 1.0
                }
                if .MovingBack in flags^ {
                    translate_vector_z += -1.0
                }
                if .MovingForward in flags^ {
                    translate_vector_z += 1.0
                }
            }

            // Input vector is in view space, so we transform to world space
            world_invector := hlsl.float4 {-translate_vector_z, translate_vector_x, 0.0, 0.0}
            world_invector = yaw_rotation_matrix(-camera.yaw) * world_invector
            if hlsl.length(world_invector) > 1.0 {
                world_invector = hlsl.normalize(world_invector)
            }

            // Now we have a representation of the player's input vector in world space

            // Handle sprint
            this_frame_move_speed := char.move_speed
            {
                flags := &char.flags
                amount, ok := output_verbs.floats[.Sprint]
                if ok {
                    this_frame_move_speed = linalg.lerp(char.move_speed, char.sprint_speed, amount)
                }
                if .Sprint in output_verbs.bools {
                    if output_verbs.bools[.Sprint] {
                        flags^ += {.Sprinting}
                    } else {
                        flags^ -= {.Sprinting}
                    }
                }
                if .Sprinting in flags {
                    this_frame_move_speed = char.sprint_speed
                }
            }

            {
                char.acceleration = {world_invector.x, world_invector.y, 0.0}
                accel_len := hlsl.length(char.acceleration)
                this_frame_move_speed *= accel_len
                // if accel_len == 0 && collision.state == .Grounded {
                //     to_zero := hlsl.float2 {0.0, 0.0} - collision.velocity.xy
                //     collision.velocity.xy += char.deceleration_speed * to_zero
                // }
                collision.velocity.xy += char.acceleration.xy

                // Limit velocity magnitude to this_frame_move_speed
                if math.abs(hlsl.length(collision.velocity.xy)) > this_frame_move_speed {
                    collision.velocity.xy = this_frame_move_speed * hlsl.normalize(collision.velocity.xy)
                }
                movement_dist := hlsl.length(collision.velocity.xy)

                duration := get_animation_duration(renderer^, model.anim_idx)
                model.anim_t += char.anim_speed * dt * movement_dist
                for model.anim_t >= duration {
                    model.anim_t -= duration
                }
            }

            if translate_vector_x != 0.0 || translate_vector_z != 0.0 {
                tform.rotation = linalg.quaternion_between_two_vector3_f32(DEFAULT_FACING_DIRECTION, hlsl.normalize(world_invector).xyz)
            }
        }

        // Handle jump command
        {
            flags := &char.flags
            if collision.state == .Grounded {
                flags^ -= {.AlreadyJumped}
            }

            jumped, jump_ok := output_verbs.bools[.PlayerJump]
            if jump_ok {
                // If jump input state changed...
                if jumped {
                    // To jumping...
                    if .AlreadyJumped in flags {
                        // And we were already jumping:
                        if .HoldingEnemy in char.flags {
                            // And we're holding an enemy
                            // Do thrown-enemy double-jump

                            pos := tform.position - {0.0, 0.0, 0.5}
                            new_thrown_enemy(game_state, pos, hlsl.float3 {0.0, 0.0, -ENEMY_THROW_SPEED}, .Wandering, char.enemy_respawn_pos, char.enemy_type)
                            char.flags -= {.HoldingEnemy}
                            collision.velocity.z = 1.3 * char.jump_speed
                            play_sound_effect(audio_system, game_state.jump_sound)
                        }
                    } else {
                        // Do first jump
                        collision.velocity.z = char.jump_speed
                        collision.state = .Falling
                        flags^ += {.AlreadyJumped}

                        play_sound_effect(audio_system, game_state.jump_sound)
                    }

                    collision.gravity_scale = CHARACTER_NORMAL_GRAVITY
                    collision.state = .Falling
                } else {
                    // To not jumping...
                    collision.gravity_scale = CHARACTER_HEAVY_GRAVITY
                }
            }
        }

        // Teleport player back to spawn if hit death plane
        respawn := output_verbs.bools[.PlayerReset]
        respawn |= tform.position.z < -50.0
        respawn |= char.health == 0
        if respawn {
            tform.position = game_state.level_start
            collision.velocity = {}
            char.acceleration = {}
            char.health = CHARACTER_MAX_HEALTH
            char.flags = {}
        }

        @static velocity_bump_x :f32= 100.0
        @static velocity_bump_z :f32= 10.0

        if imgui.Begin("Dash velocity tinkering") {
            imgui.SliderFloat3("Player velocity", &collision.velocity, -10.0, 10.0)

            imgui.SliderFloat("Player velocity bump x", &velocity_bump_x, -1000.0, 100.0)
            imgui.SliderFloat("Player velocity bump z", &velocity_bump_z, -1000.0, 100.0)
        }

        imgui.End()
        // Shoot command
        {
            res, have_shoot := output_verbs.bools[.PlayerShoot]
            if have_shoot && res {
                if .HoldingEnemy in char.flags {
                    char.flags -= {.HoldingEnemy}
                    throw_dir := ENEMY_THROW_SPEED * linalg.quaternion128_mul_vector3(tform.rotation, DEFAULT_FACING_DIRECTION)
                    // throw_dir := ENEMY_THROW_SPEED * linalg.quaternion128_mul_vector3(tform.rotation,  DEFAULT_FACING_DIRECTION)
                    new_thrown_enemy(game_state, tform.position, throw_dir, .Wandering, char.enemy_respawn_pos, char.enemy_type)
                    throw_rebound_dir := throw_dir
                    // enemy_throw_dir += {velocity_bump_x, 0, velocity_bump_z}
                    // collision.velocity = {velocity_bump_x, 0, velocity_bump_z}
                    // lanch player backwards on enemy forwards throw
                    // collision.velocity = 1000 * throw_rebound_dir
                    collision.velocity = velocity_bump_x * throw_rebound_dir
                    collision.velocity += {0.0, 0.0, velocity_bump_z}
                    play_sound_effect(audio_system, game_state.shoot_sound)
                } else {
                    char.vortex_t = 0.0
                }
            }
        }

        player_sphere := Sphere {
            position = tform.position,
            radius = collision.radius
        }

        // Check player against coins
        {
            to_remove: Maybe(u32)
            for coin_id, idx in game_state.coins {
                coin_tform := &game_state.transforms[coin_id]
                {
                    // Are we being collected?
                    s := Sphere {
                        position = coin_tform.position,
                        radius = coin_tform.scale
                    }
                    if are_spheres_overlapping(player_sphere, s) {
                        play_sound_effect(audio_system, game_state.coin_sound)
                        to_remove = u32(idx)
                        continue
                    }
                }
            }

            // Remove coin
            remove_idx, ok := to_remove.?
            if ok {
                remove_id := game_state.coins[remove_idx]
                delete_key(&game_state.transforms, remove_id)
                delete_key(&game_state.static_models, remove_id)
                unordered_remove(&game_state.coins, remove_idx)
            }
        }

        // Do logic for vortex move
        if char.vortex_t < char.bullet_travel_time {
            char.vortex_t += dt
            radius := VORTEX_MAX_RADIUS * char.vortex_t / char.bullet_travel_time

            // Update graphics
            mat := scaling_matrix(radius)
            mat[3][0] = tform.position.x
            mat[3][1] = tform.position.y
            mat[3][2] = tform.position.z
            draw := DebugDraw {
                world_from_model = mat,
                color = {0.0, 0.4, 0.0, 0.3}
            }
            draw_debug_mesh(renderer, game_state.sphere_mesh, &draw)

            do_point_light(renderer, PointLight {
                world_position = tform.position,
                intensity = radius,
                color = {0.0, 1.0, 0.0}
            })
        }

        if .HoldingEnemy in char.flags {
            bob := 0.2 * math.sin(game_state.time * 1.7)
            pos := tform.position + {0.0, 0.0, 1.75 + bob}
            m := yaw_rotation_matrix(game_state.time) * uniform_scaling_matrix(0.5)
            m[3][0] = pos.x
            m[3][1] = pos.y
            m[3][2] = pos.z
            d := StaticDraw {
                world_from_model = m,
                flags = {.Glowing}
            }
            draw_ps1_static_mesh(renderer, game_state.enemy_mesh, d)

            // Light source
            l := default_point_light()
            l.color = {0.0, 1.0, 0.0}
            l.world_position = pos
            l.intensity = light_flicker(game_state.rng_seed, game_state.time)
            do_point_light(renderer, l)
        }
    }
}

StaticModelInstance :: struct {
    handle: StaticModelHandle,
    pos_offset: hlsl.float3,
    flags: InstanceFlags,
}

SkinnedModelInstance :: struct {
    handle: SkinnedModelHandle,
    pos_offset: hlsl.float3,
    anim_idx: u32,
    anim_t: f32,
    flags: InstanceFlags,
}

DebugModelInstance :: struct {
    handle: StaticModelHandle,
    pos_offset: hlsl.float3,
    color: hlsl.float4,
    scale: f32,
}

HitEvent :: struct {
    player_id: EntityID     // Which player was hit
}

process_hit_events :: proc(game_state: ^GameState, audio_system: ^AudioSystem) {
    scoped_event(&profiler, "process_hit_events")
    for event in game_state.character_hit_events {
        collision := &game_state.spherical_bodies[event.player_id]
        char := &game_state.character_controllers[event.player_id]
        invulnerable := !timer_expired(char.time_last_damaged, CHARACTER_INVULNERABILITY_DURATION * SECONDS_TO_NANOSECONDS)

        // If character is falling, then process this as jumping on enemy
        if !invulnerable && collision.velocity.z < 0.0 {
            collision.velocity.z = 20.0
            char.time_last_damaged = time.now()
            continue
        }

        // Otherwise process as player damage
        if invulnerable {
            continue
        }
        char.time_last_damaged = time.now()
        collision.velocity.z = 3.0
        collision.state = .Falling
        char.health -= 1
        play_sound_effect(audio_system, game_state.ow_sound)
    }
}

MovedEntityEvent :: struct {
    id: EntityID,
    old_tform: Transform,
}

tick_moved_entity :: proc(game_state: ^GameState) {
    scoped_event(&profiler, "tick_moved_entity")

    for event in game_state.moved_collision {
        tform, ok := &game_state.transforms[event.id]
        assert(ok)

        delta := Transform {
            position = tform.position - event.old_tform.position,
            rotation = tform.rotation - event.old_tform.rotation,
            scale = tform.scale - event.old_tform.scale,
        }
        children := get_entity_children(game_state^, event.id, context.temp_allocator)
        for child in children {
            apply_transform_delta(game_state, child, delta)
        }

        mesh, mesh_ok := &game_state.triangle_meshes[event.id]
        if mesh_ok {
            mmat := get_transform_matrix(tform^)
            mesh.model_matrix = mmat
            rebuild_static_triangle_mesh(mesh, mmat)
        }
    }
}

draw_static_models :: proc(game_state: ^GameState, renderer: ^Renderer) {
    scoped_event(&profiler, "Draw static models")
    for id, model in game_state.static_models {
        tform := &game_state.transforms[id]

        mat := get_transform_matrix(tform^)
        mat[3][0] += model.pos_offset.x
        mat[3][1] += model.pos_offset.y
        mat[3][2] += model.pos_offset.z
        draw := StaticDraw {
            world_from_model = mat,
            flags = model.flags
        }
        draw_ps1_static_mesh(renderer, model.handle, draw)

        if .Glowing in model.flags {
            // Light source
            l := default_point_light()
            l.world_position = tform.position
            l.color = {0.0, 1.0, 0.0}
            l.intensity = light_flicker(game_state.rng_seed, game_state.time)
            do_point_light(renderer, l)
        }
    }
}

draw_skinned_models :: proc(game_state: ^GameState, renderer: ^Renderer) {
    scoped_event(&profiler, "Draw skinned models")
    // Draw skinned models
    for id, model in game_state.skinned_models {
        tform := &game_state.transforms[id]

        mat := get_transform_matrix(tform^)
        mat[3][0] += model.pos_offset.x
        mat[3][1] += model.pos_offset.y
        mat[3][2] += model.pos_offset.z
        draw := SkinnedDraw {
            world_from_model = mat,
            anim_idx = model.anim_idx,
            anim_t = model.anim_t,
            //flags = model.flags
        }
        draw_ps1_skinned_mesh(renderer, model.handle, &draw)

        if .Glowing in model.flags {
            // Light source
            l := default_point_light()
            l.world_position = tform.position
            l.color = {0.0, 1.0, 0.0}
            l.intensity = light_flicker(game_state.rng_seed, game_state.time)
            do_point_light(renderer, l)
        }
    }
}

draw_debug_models :: proc(game_state: ^GameState, renderer: ^Renderer) {
    scoped_event(&profiler, "Draw debug models")
    // Draw debug models
    for id, model in game_state.debug_models {
        tform := &game_state.transforms[id]

        mat := get_transform_matrix(tform^, model.scale)
        mat[3][0] += model.pos_offset.x
        mat[3][1] += model.pos_offset.y
        mat[3][2] += model.pos_offset.z
        draw := DebugDraw {
            world_from_model = mat,
            color = model.color
        }
        draw_debug_mesh(renderer, game_state.sphere_mesh, &draw)
    }
}

CollisionState :: enum {
    Grounded,
    Falling
}

EnemyState :: enum {
    BrainDead,

    Wandering,
    Resting,

    AlertedBounce,
    AlertedCharge,
}
ENEMY_STATE_CSTRINGS :: [EnemyState]cstring {
    .BrainDead = "Brain Dead",
    .Wandering = "Wandering",
    .Resting = "Resting",
    .AlertedBounce = "Alerted Bounce",
    .AlertedCharge = "Alerted Charge"
}

DebugVisualizationFlag :: enum {
    ShowPlayerHitSphere,
    ShowPlayerActivityRadius,
    ShowCoinRadius,
    ShowBoundingSpheres,
}
DebugVisualizationFlags :: bit_set[DebugVisualizationFlag]

LevelBlock :: enum u8 {
    Terrain = 0,
    StaticScenery = 1,
    AnimatedScenery = 2,
    Enemies = 3,
    BgmFile = 4,
    DirectionalLights = 5,
    Coins = 6,
}

delete_entity :: proc(game_state: ^GameState, id: EntityID) {
    delete_key(&game_state.transforms, id)
    delete_key(&game_state.transform_deltas, id)
    delete_key(&game_state.cameras, id)
    delete_key(&game_state.lookat_controllers, id)
    delete_key(&game_state.character_controllers, id)
    delete_key(&game_state.enemy_ais, id)
    delete_key(&game_state.hovering_enemies, id)
    delete_key(&game_state.thrown_enemy_ais, id)
    delete_key(&game_state.spherical_bodies, id)
    delete_key(&game_state.triangle_meshes, id)
    delete_key(&game_state.static_models, id)
    delete_key(&game_state.skinned_models, id)
    delete_key(&game_state.debug_models, id)
    delete_key(&game_state.bounding_spheres, id)
    delete_key(&game_state.parents, id)

    idx_to_delete: Maybe(int)
    for id2, i in game_state.looping_animations {
        if id == id2 {
            idx_to_delete = i
        }
    }
    if idx, ok := idx_to_delete.? ; ok {
        unordered_remove(&game_state.looping_animations, idx)
        idx_to_delete = nil
    }
    for id2, i in game_state.coins {
        if id == id2 {
            idx_to_delete = i
        }
    }
    if idx, ok := idx_to_delete.? ; ok {
        unordered_remove(&game_state.coins, idx)
    }
}

get_entity_children :: proc(game_state: GameState, id: EntityID, allocator := context.allocator) -> [dynamic]EntityID {
    kids := make([dynamic]EntityID, 0, 64, allocator)

    for child_id, parent_id in game_state.parents {
        if id == parent_id {
            append(&kids, child_id)
        }
    }

    return kids
}

apply_transform_delta :: proc(game_state: ^GameState, id: EntityID, tform_delta: Transform) {
    tform, ok := &game_state.transforms[id]
    assert(ok)

    tform.position += tform_delta.position
    tform.scale += tform_delta.scale
}

EntityID :: distinct u32

// Megastruct for all game-specific data
GameState :: struct {
    local_players: [dynamic; MAX_SPLITSCREEN_PLAYERS]EntityID,
    viewport_cameras: [dynamic; MAX_SPLITSCREEN_PLAYERS]EntityID,

    // Scene/Level data
    level_start: hlsl.float3,
    skybox_texture: vkw.Image_Handle,

    // Game data as relational database rows
    _next_id: u32,                   // Components with the same id are associated with one another
    transforms: map[EntityID]Transform,
    transform_deltas: map[EntityID]TransformDelta,
    cameras: map[EntityID]FreecamController,
    lookat_controllers: map[EntityID]LookatController,
    character_controllers: map[EntityID]CharacterController,
    enemy_ais: map[EntityID]EnemyAI,
    hovering_enemies: map[EntityID]HoveringEnemy,
    thrown_enemy_ais: map[EntityID]ThrownEnemyAI,
    spherical_bodies: map[EntityID]SphericalBody,
    triangle_meshes: map[EntityID]TriangleMesh,
    static_models: map[EntityID]StaticModelInstance,
    skinned_models: map[EntityID]SkinnedModelInstance,
    debug_models: map[EntityID]DebugModelInstance,
    bounding_spheres: map[EntityID]Sphere,
    parents: map[EntityID]EntityID,

    // Sometimes we need behavior associated with a group of ids
    // without actually needing to store additional state
    looping_animations: [dynamic]EntityID,
    coins: [dynamic]EntityID,

    character_hit_events: [dynamic]HitEvent,
    moved_collision: [dynamic]MovedEntityEvent,

    // User input mapping structs
    system_key_mappings: map[sdl2.Scancode]VerbType,
    freecam_key_mappings : map[sdl2.Scancode]VerbType,
    character_key_mappings: map[sdl2.Scancode]VerbType,
    character_menu_key_mappings: map[sdl2.Scancode]VerbType,
    ctrl_key_mappings: map[sdl2.Scancode]VerbType,
    mouse_mappings: map[u8]VerbType,
    button_mappings: [len(VerbRecipient)]map[sdl2.GameControllerButton]VerbType,
    menu_button_mappings: [len(VerbRecipient)]map[sdl2.GameControllerButton]VerbType,
    system_button_mappings: map[sdl2.GameControllerButton]VerbType,

    // Icosphere mesh for visualizing spherical collision and points
    sphere_mesh: StaticModelHandle,

    coin_mesh: StaticModelHandle,
    collectable_radius: f32,

    enemy_mesh: StaticModelHandle,
    plane_mesh: StaticModelHandle,

    debug_vis_flags: DebugVisualizationFlags,
    edit_flags: EditFlags,

    // Global sound effects loaded on init_gamestate()
    bgm_id: int,
    jump_sound: int,
    shoot_sound: int,
    coin_sound: int,
    ow_sound: int,

    camera_follow_speed: f32,
    timescale: f32,
    time: f32,
    rng_seed: i64,

    freecam_collision: bool,
    freecam_speed_multiplier: f32,
    freecam_slow_multiplier: f32,

    borderless_fullscreen: bool,
    exclusive_fullscreen: bool,

    paused: bool,
}

init_gamestate :: proc(
    gd: ^vkw.VulkanGraphicsDevice,
    renderer: ^Renderer,
    audio_system: ^AudioSystem,
    user_config: ^UserConfiguration,
    global_allocator: runtime.Allocator,
) -> GameState {
    scoped_event(&profiler, "Init gamestate")
    game_state: GameState
    game_state.freecam_collision = user_config.flags[.FreecamCollision]
    game_state.borderless_fullscreen = user_config.flags[.BorderlessFullscreen]
    game_state.exclusive_fullscreen = user_config.flags[.ExclusiveFullscreen]
    game_state.paused = false
    game_state.timescale = 1.0
    game_state.collectable_radius = 0.1

    game_state.system_key_mappings = make(map[sdl2.Scancode]VerbType, allocator = global_allocator)
    game_state.freecam_key_mappings = make(map[sdl2.Scancode]VerbType, allocator = global_allocator)
    game_state.character_key_mappings = make(map[sdl2.Scancode]VerbType, allocator = global_allocator)
    game_state.character_menu_key_mappings = make(map[sdl2.Scancode]VerbType, allocator = global_allocator)
    game_state.ctrl_key_mappings = make(map[sdl2.Scancode]VerbType, allocator = global_allocator)
    game_state.mouse_mappings = make(map[u8]VerbType, 64, allocator = global_allocator)
    for r in VerbRecipient {
        game_state.button_mappings[r] = make(map[sdl2.GameControllerButton]VerbType, 64, allocator = global_allocator)
        game_state.menu_button_mappings[r] = make(map[sdl2.GameControllerButton]VerbType, 64, global_allocator)
    }
    game_state.system_button_mappings = make(map[sdl2.GameControllerButton]VerbType, 64, allocator = global_allocator)

    {
        game_state.system_key_mappings[.ESCAPE] = .ToggleImgui
        game_state.system_key_mappings[.BACKSLASH] = .FrameAdvance
        game_state.system_key_mappings[.PAUSE] = .TogglePause
        game_state.system_key_mappings[.F] = .FullscreenHotkey
        
        game_state.freecam_key_mappings[.W] = .TranslateFreecamForward
        game_state.freecam_key_mappings[.S] = .TranslateFreecamBack
        game_state.freecam_key_mappings[.A] = .TranslateFreecamLeft
        game_state.freecam_key_mappings[.D] = .TranslateFreecamRight
        game_state.freecam_key_mappings[.Q] = .TranslateFreecamDown
        game_state.freecam_key_mappings[.E] = .TranslateFreecamUp
        game_state.freecam_key_mappings[.LSHIFT] = .Sprint
        game_state.freecam_key_mappings[.LCTRL] = .Crawl
        
        game_state.character_key_mappings[.W] = .PlayerTranslateForward
        game_state.character_key_mappings[.S] = .PlayerTranslateBack
        game_state.character_key_mappings[.A] = .PlayerTranslateLeft
        game_state.character_key_mappings[.D] = .PlayerTranslateRight
        game_state.character_key_mappings[.LSHIFT] = .Sprint
        game_state.character_key_mappings[.LCTRL] = .Crawl
        game_state.character_key_mappings[.SPACE] = .PlayerJump
        game_state.character_key_mappings[.R] = .PlayerReset
        game_state.character_key_mappings[.E] = .PlayerShoot
        game_state.character_key_mappings[.RETURN] = .PlayerPauseGame

        game_state.character_menu_key_mappings[.RETURN] = .PlayerPauseGame

        game_state.ctrl_key_mappings[.N] = .NewLevel
        game_state.ctrl_key_mappings[.L] = .ShowLoadLevel
        game_state.ctrl_key_mappings[.MINUS] = .ImguiScaleDown
        game_state.ctrl_key_mappings[.EQUALS] = .ImguiScaleUp

        for recipient in VerbRecipient {
            game_state.button_mappings[recipient][.A] = .PlayerJump
            game_state.button_mappings[recipient][.X] = .PlayerShoot
            game_state.button_mappings[recipient][.Y] = .PlayerReset
            game_state.button_mappings[recipient][.START] = .PlayerPauseGame
            game_state.button_mappings[recipient][.LEFTSHOULDER] = .TranslateFreecamDown
            game_state.button_mappings[recipient][.RIGHTSHOULDER] = .TranslateFreecamUp

            game_state.menu_button_mappings[recipient][.B] = .PopMenu
            game_state.menu_button_mappings[recipient][.START] = .PlayerPauseGame
        }

        game_state.system_button_mappings[.START] = .AddLocalPlayer


        // Hardcoded default mouse mappings
        game_state.mouse_mappings[sdl2.BUTTON_RIGHT] = .ToggleMouseLook
    }

    game_state.rng_seed = time.now()._nsec

    idxs, ok := load_sound_effects(
        audio_system,
        {"data/audio/boing.ogg", "data/audio/shoot.ogg", "data/audio/orb_final.ogg", "data/audio/ow.ogg"},
        global_allocator,
        context.temp_allocator
    )
    if !ok {
        log.error("Error loading sound effects.")
    }

    game_state.jump_sound = idxs[0]
    game_state.shoot_sound = idxs[1]
    game_state.coin_sound = idxs[2]
    game_state.ow_sound = idxs[3]

    // Load skybox
    {
        scoped_event(&profiler, "Load skybox")
        // @TODO: Load this from level file
        path := "data/images/beach.dds"
        file_bytes, image_err := os.read_entire_file_from_path(path, context.allocator)

        if image_err == nil {
            // Read DDS header
            dds_header, dds_ok := dds_load_header(file_bytes)
            if !dds_ok {
                log.error("Unable to read DDS header")
            }

            is_cubemap := .D3D11_RESOURCE_MISC_TEXTURECUBE in dds_header.misc_flag
            assert(is_cubemap)
            image_flags : vk.ImageCreateFlags = {.CUBE_COMPATIBLE} if is_cubemap else {}
            image_format := dxgi_to_vulkan_format(dds_header.dxgi_format)
            image_info := vkw.Image_Create {
                flags = image_flags,
                image_type = .D2,
                format = image_format,
                extent = {
                    width = dds_header.width,
                    height = dds_header.height,
                    depth = dds_header.depth,
                },
                has_mipmaps = dds_header.mipmap_count > 1,
                mip_count = dds_header.mipmap_count,
                array_layers = 6,
                samples = {._1},
                tiling = .OPTIMAL,
                usage = {.SAMPLED},
                alloc_flags = nil,
                name = "Skybox"
            }
            image_bytes := file_bytes[TRUE_DDS_HEADER_SIZE:]
            image_handle, create_ok := vkw.sync_create_image_with_data(gd, &image_info, image_bytes[:])

            if create_ok {
                renderer.uniforms.skybox_idx = image_handle.idx
            }
        }
    }

    return game_state
}

gamestate_new_scene :: proc(
    game_state: ^GameState,
    gd: ^vkw.VulkanGraphicsDevice,
    renderer: ^Renderer,
    user_config: ^UserConfiguration,
    scene_allocator := context.allocator
) {
    clear(&game_state.local_players)
    clear(&game_state.viewport_cameras)

    // Initialize data-oriented tables
    game_state._next_id = 0                 // All entities are deleted on new_scene(), so set ids back to 0
    game_state.transforms = make(map[EntityID]Transform, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.transform_deltas = make(map[EntityID]TransformDelta, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.cameras = make(map[EntityID]FreecamController, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.lookat_controllers = make(map[EntityID]LookatController, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.character_controllers = make(map[EntityID]CharacterController, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.enemy_ais = make(map[EntityID]EnemyAI, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.hovering_enemies = make(map[EntityID]HoveringEnemy, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.thrown_enemy_ais = make(map[EntityID]ThrownEnemyAI, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.spherical_bodies = make(map[EntityID]SphericalBody, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.triangle_meshes = make(map[EntityID]TriangleMesh, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.static_models = make(map[EntityID]StaticModelInstance, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.skinned_models = make(map[EntityID]SkinnedModelInstance, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.debug_models = make(map[EntityID]DebugModelInstance, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.bounding_spheres = make(map[EntityID]Sphere, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.parents = make(map[EntityID]EntityID, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)

    game_state.looping_animations = make([dynamic]EntityID, 0, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)
    game_state.coins = make([dynamic]EntityID, 0, DEFAULT_COMPONENT_MAP_CAPACITY, scene_allocator)

    // Initialize player character
    {
        player_id := new_local_player_character(game_state, renderer, user_config^, scene_allocator)
        append(&game_state.local_players, player_id)
        {
            path : cstring = "data/models/CesiumMan.glb"
            skinned_model := load_gltf_skinned_model(renderer, path, scene_allocator)
            game_state.skinned_models[player_id] = SkinnedModelInstance {
                handle = skinned_model,
                pos_offset = {0.0, 0.0, -0.6},
                flags = {}
            }
        }

    }

    game_state.freecam_speed_multiplier = 5.0
    game_state.freecam_slow_multiplier = 1.0 / 5.0

    //game_state.camera_follow_point = game_state.character.collision.position
    game_state.camera_follow_speed = 6.0

    // Load icosphere mesh for debug visualization
    game_state.sphere_mesh = load_gltf_static_model(gd, renderer, "data/models/icosphere.glb", scene_allocator)

    // Load enemy mesh
    game_state.enemy_mesh = load_gltf_static_model(gd, renderer, "data/models/majoras_moon.glb", scene_allocator)

    game_state.coin_mesh = load_gltf_static_model(gd, renderer, "data/models/precursor_orb.glb", scene_allocator)

    game_state.plane_mesh = load_gltf_static_model(gd, renderer, "data/models/plane.glb", scene_allocator)
}

gamestate_next_id :: proc(gamestate: ^GameState) -> EntityID {
    assert(gamestate._next_id < max(u32), "Overflowed entity id!")
    r := gamestate._next_id
    gamestate._next_id += 1
    return EntityID(r)
}

game_tick :: proc(game_state: ^GameState, renderer: ^Renderer, output_verbs: OutputVerbs, audio_system: ^AudioSystem, dt: f32) {
    scoped_event(&profiler, "GameState tick")

    // Determine if we're simulating a tick of game logic this frame
    system_verbs := output_verbs.recipient_verbs[VerbRecipient.System]
    do_this_frame := !game_state.paused
    if system_verbs.bools[.FrameAdvance] {
        do_this_frame = true
        game_state.paused = true
    }

    if do_this_frame {
        // Advance game_state by dt seconds
        tick_character_controllers(game_state, renderer, output_verbs, audio_system, dt)
        tick_coins(game_state, audio_system)
        tick_looping_animations(game_state, renderer^, dt)
        tick_transform_deltas(game_state, dt)
        tick_thrown_enemies(game_state)
        tick_spherical_bodies(game_state, dt)
        tick_enemy_ai(game_state, audio_system, dt)
        tick_hovering_enemies(game_state, dt)
        process_hit_events(game_state, audio_system)
        tick_moved_entity(game_state)
    }

    // Draw commands
    draw_static_models(game_state, renderer)
    draw_skinned_models(game_state, renderer)
    draw_debug_models(game_state, renderer)

    // Recreate per-frame dynamic allocations
    game_state.character_hit_events = make([dynamic]HitEvent, 0, DEFAULT_COMPONENT_MAP_CAPACITY, context.temp_allocator)
    game_state.moved_collision = make([dynamic]MovedEntityEvent, 0, DEFAULT_COMPONENT_MAP_CAPACITY, context.temp_allocator)
}

// Returns the size in bytes of component when serialized
get_serialized_size :: proc(renderer: ^Renderer, string_table: ^StringTable, component: $ComponentType) -> int {
    lil_helper :: proc(string_table: ^StringTable, str: string) -> int {
        // This proc returns the size in bytes of _this_
        // instance of the string in the level file
        // u32 offset + length
        size := 2 * size_of(u32)
        seen_it := str in string_table.string_map
        if !seen_it {
            // Only if this is the first time seeing this string
            // do we want to add it's length to the total size
            size += len(str)
            string_table_append(string_table, str)
        }

        return size
    }

    size := size_of(EntityID)

    when ComponentType == TriangleMesh {
        size += size_of(hlsl.float4x4)
        size += lil_helper(string_table, component.name)
    } else when ComponentType == StaticModelInstance {
        size += size_of(component.pos_offset)
        size += size_of(component.flags)

        // Size of string instance
        model := get_static_model(renderer, component.handle)
        size += lil_helper(string_table, model.name)
    } else when ComponentType == SkinnedModelInstance {
        size += size_of(component.pos_offset)
        size += size_of(component.flags)
        size += size_of(component.anim_idx)

        // Size of string instance
        model := get_skinned_model(renderer, component.handle)
        size += lil_helper(string_table, model.name)
    } else when ComponentType == DebugModelInstance {
        size += size_of(component.pos_offset)
        size += size_of(component.color)
        size += size_of(component.scale)

        // Size of string instance
        model := get_static_model(renderer, component.handle)
        size += lil_helper(string_table, model.name)
    } else {
        // Type doesn't need special handling
        size += size_of(ComponentType)
    }

    return size
}

LEVEL_FILE_MAGIC_STRING :: "katawari"

load_level_file :: proc(
    app: ^App,
    path: string,
    scene_allocator := context.allocator
) -> bool {
    // Audio lock while loading level data
    sdl2.LockAudioDevice(app.audio_system.device_id)
    defer sdl2.UnlockAudioDevice(app.audio_system.device_id)

    vkw.device_wait_idle(&app.vgd)

    read_head : u32 = 0

    lvl_data: []byte
    {
        err: os.Error
        lvl_data, err = os.read_entire_file_from_path(path, context.temp_allocator)
        if err != nil {
            log.errorf("Error reading entire level file \"%v\": %v", path, err)
            return false
        }
    }

    // Read magic string
    magic := read_string_from_buffer(lvl_data, &read_head)
    if magic != LEVEL_FILE_MAGIC_STRING {
        log.errorf("%v has wrong magic string. Aborting level load.", path)
        return false
    }

    // Have to free rendering resources before scene_allocator is reset
    renderer_free_resources(&app.renderer)
    free_all(scene_allocator)
    audio_new_scene(&app.audio_system)
    renderer_new_scene(&app.renderer, scene_allocator)
    gamestate_new_scene(&app.game_state, &app.vgd, &app.renderer, &app.user_config)

    read_thing_from_buffer :: proc(buffer: []byte, $type: typeid, read_head: ^u32) -> type {
        thing: type
        mem.copy_non_overlapping(&thing, &buffer[read_head^], size_of(type))
        read_head^ += size_of(type)
        return thing
    }

    read_string_from_buffer :: proc(buffer: []byte, read_head: ^u32) -> string {
        // Read the u32 string length, then read the string itself
        str_len := read_thing_from_buffer(buffer, u32, read_head)
        s := strings.string_from_ptr(&buffer[read_head^], int(str_len))
        read_head^ += str_len
        return s
    }

    read_naked_string_from_buffer :: proc(buffer: []byte, offset: u32, length: u32) -> string {
        // Precondition: buffer should start at the first byte of the string table

        start_ptr := slice.ptr_add(&buffer[0], int(offset))
        return strings.string_from_ptr(start_ptr, int(length))
    }

    read_component_map :: proc(
        gd: ^vkw.VulkanGraphicsDevice,
        renderer: ^Renderer,
        buffer: []byte,
        components: ^map[EntityID]$T,
        head: ^u32,
        string_table_offset: u32,
        largest_seen_id: ^u32,
        scene_allocator: runtime.Allocator
    ) {
        // Read component count
        count := read_thing_from_buffer(buffer, u32, head)

        get_model_path :: proc(
            buffer: []byte,
            head: ^u32,
            string_table_offset: u32,
            scene_allocator: runtime.Allocator
        ) -> cstring {
            sb: strings.Builder
            strings.builder_init(&sb, scene_allocator)

            // Read offset and length of model string and then load it
            offset := read_thing_from_buffer(buffer, u32, head)
            length := read_thing_from_buffer(buffer, u32, head)
            model_string := read_naked_string_from_buffer(buffer[string_table_offset:], offset, length)
            fmt.sbprintf(&sb, "data/models/%v", model_string)
            return strings.to_cstring(&sb)
        }

        for _ in 0..<count {
            id := read_thing_from_buffer(buffer, EntityID, head)

            comp: T
            when T == TriangleMesh {
                mmat := read_thing_from_buffer(buffer, hlsl.float4x4, head)
                model_path := get_model_path(buffer, head, string_table_offset, scene_allocator)

                comp = load_static_triangle_mesh(string(model_path), mmat, scene_allocator)

            } else when T == StaticModelInstance {
                comp.pos_offset = read_thing_from_buffer(buffer, hlsl.float3, head)
                comp.flags = read_thing_from_buffer(buffer, InstanceFlags, head)
                model_path := get_model_path(buffer, head, string_table_offset, scene_allocator)
                comp.handle = load_gltf_static_model(gd, renderer, model_path, scene_allocator)
            } else when T == SkinnedModelInstance {
                comp.pos_offset = read_thing_from_buffer(buffer, hlsl.float3, head)
                comp.flags = read_thing_from_buffer(buffer, InstanceFlags, head)
                comp.anim_idx = read_thing_from_buffer(buffer, u32, head)
                model_path := get_model_path(buffer, head, string_table_offset, scene_allocator)
                comp.handle = load_gltf_skinned_model(renderer, model_path, scene_allocator)
            } else when T == DebugModelInstance {
                comp.pos_offset = read_thing_from_buffer(buffer, hlsl.float3, head)
                comp.color = read_thing_from_buffer(buffer, hlsl.float4, head)
                comp.scale = read_thing_from_buffer(buffer, f32, head)
                model_path := get_model_path(buffer, head, string_table_offset, scene_allocator)
                comp.handle = load_gltf_static_model(gd, renderer, model_path, scene_allocator)
            } else {
                comp = read_thing_from_buffer(buffer, T, head)
            }

            components[id] = comp

            if u32(id) > largest_seen_id^ {
                largest_seen_id^ = u32(id)
            }
        }
    }

    read_stateless_entities :: proc(buffer: []byte, head: ^u32) -> [dynamic]EntityID {
        ids: [dynamic]EntityID

        size := read_thing_from_buffer(buffer, u32, head)
        if size == 0 {
            return ids
        }
        resize(&ids, size)

        len_bytes := size * size_of(EntityID)
        mem.copy_non_overlapping(&ids[0], &buffer[head^], int(len_bytes))
        head^ += len_bytes

        return ids
    }

    path_builder: strings.Builder
    strings.builder_init(&path_builder, context.temp_allocator)

    largest_saved_entity_id: u32 = 0

    // Read string table global offset
    string_table_offset := read_thing_from_buffer(lvl_data, u32, &read_head)

    // Read player spawn position
    app.game_state.level_start = read_thing_from_buffer(lvl_data, hlsl.float3, &read_head)

    // Read bgm name
    {
        bgm_name := read_string_from_buffer(lvl_data, &read_head)
        fmt.sbprintf(&path_builder, "data/audio/%v.ogg", bgm_name)
        path := strings.to_cstring(&path_builder)
        app.game_state.bgm_id, _ = open_music_file(&app.audio_system, path)
        strings.builder_reset(&path_builder)
    }

    // Read directional light data
    {
        count := read_thing_from_buffer(lvl_data, u32, &read_head)
        app.renderer.directional_light_count = count
        for i in 0..<count {
            light := read_thing_from_buffer(lvl_data, NewDirectionalLight, &read_head)
            app.renderer.directional_lights[i] = NewDirectionalLight {
                yaw = light.yaw,
                pitch = light.pitch,
                color = light.color
            }
        }
    }

    // Read components in order
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.transforms, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.transform_deltas, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.enemy_ais, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.hovering_enemies, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.thrown_enemy_ais, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.spherical_bodies, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.triangle_meshes, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.static_models, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.skinned_models, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)
    read_component_map(&app.vgd, &app.renderer, lvl_data, &app.game_state.debug_models, &read_head, string_table_offset, &largest_saved_entity_id, scene_allocator)

    // Read stateless entities
    app.game_state.looping_animations = read_stateless_entities(lvl_data, &read_head)
    app.game_state.coins = read_stateless_entities(lvl_data, &read_head)

    // Should have read entire buffer
    assert(read_head == string_table_offset)

    app.game_state._next_id = largest_saved_entity_id + 1

    path_base := filepath.stem(path)
    path_clone, err := strings.clone(path_base, scene_allocator)
    if err != nil {
        log.errorf("Error allocating current_level_path string: %v", err)
    }
    app.current_level = path_clone

    // Move players to spawn
    for id in app.game_state.local_players {
        tform := &app.game_state.transforms[id]
        tform.position = app.game_state.level_start
    }

    return true
}

_reencode_level_file :: proc(app: ^App, level_name: string, temp_allocator := context.temp_allocator) {
    sb: strings.Builder
    strings.builder_init(&sb, temp_allocator)

    out_path := fmt.sbprintf(&sb, "data/levels/%v_new.lvl", level_name)
    save_level_file(app, out_path, temp_allocator)
}

_reencode_level_files :: proc(app: ^App, temp_allocator := context.temp_allocator) {
    w: os.Walker
    os.walker_init_path(&w, "data/levels")
	defer os.walker_destroy(&w)

    for info in os.walker_walk(&w) {
        _reencode_level_file(app, os.stem(info.name), temp_allocator)
    }
}

// Strings in the StringTable are written back-to-back when serialized
// Components can have a pair of u32 (offset, size) to address into it
StringTable :: struct {
    data: [dynamic]StringTableEntry,
    string_map: map[string]int,
    total_len: int
}
StringTableEntry :: struct {
    str: string,
    offset: u32,
}
string_table_init :: proc(capacity: int, allocator := context.allocator) -> StringTable {
    table: StringTable
    table.data = make([dynamic]StringTableEntry, 0, capacity, allocator)
    table.string_map = make(map[string]int, capacity, allocator)
    return table
}
string_table_append :: proc(table: ^StringTable, elem: string) -> StringTableEntry {
    idx, ok := table.string_map[elem]
    if ok {
        return table.data[idx]
    } else {
        entry: StringTableEntry
        l := len(elem)
        entry.str = elem
        entry.offset = u32(table.total_len)
        table.total_len += l
        table.string_map[elem] = len(table.data)
        append(&table.data, entry)
        return entry
    }
}
write_string_table_to_buffer :: proc(buffer: []byte, table: StringTable, head: ^u32) {
    // Because each component stores the offset and length of the strings in the table,
    // we just have to write out each string back-to-back
    for entry in table.data {
        str_len := len(entry.str)
        mem.copy_non_overlapping(&buffer[head^], raw_data(entry.str), str_len)
        head^ += u32(str_len)
    }
}

save_level_file :: proc(
    app: ^App,
    path: string,
    temp_allocator := context.temp_allocator
) {
    calc_level_file_size :: proc(
        game_state: GameState,
        renderer: ^Renderer,
        audio_system: AudioSystem,
        string_table: ^StringTable
    ) -> u32 {
        calc_component_map_size :: proc(game_state: GameState, renderer: ^Renderer, string_table: ^StringTable, component_map: map[EntityID]$T) -> int {
            size := size_of(u32)
            for _, comp in component_map {
                size += get_serialized_size(renderer, string_table, comp)
            }
            return size
        }

        final_size := 0

        // Magic string
        final_size += size_of(u32)
        final_size += len(LEVEL_FILE_MAGIC_STRING)

        // Global offset of string table
        final_size += size_of(u32)

        // Size of player spawn position
        final_size += size_of(hlsl.float3)

        bgm_string: string
        if len(audio_system.music_files) > int(game_state.bgm_id) {
            bgm_string = audio_system.music_files[game_state.bgm_id].name
        }

        // Size of bgm pascal string
        final_size += size_of(u32)
        final_size += size_of(byte) * len(bgm_string)

        // Directional lights count + data
        final_size += size_of(u32)
        final_size += size_of(NewDirectionalLight) * int(renderer.directional_light_count)

        // Component data + counts
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.transforms)
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.transform_deltas)
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.enemy_ais)
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.hovering_enemies)
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.thrown_enemy_ais)
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.spherical_bodies)
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.triangle_meshes)
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.static_models)
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.skinned_models)
        final_size += calc_component_map_size(game_state, renderer, string_table, game_state.debug_models)

        // Special entities that don't need extra state
        final_size += size_of(u32)
        final_size += len(game_state.looping_animations) * size_of(EntityID)
        final_size += size_of(u32)
        final_size += len(game_state.coins) * size_of(EntityID)

        // Don't need to compute string table size explicitly bc
        // string sizes are accounted for in get_serialized_size()

        return u32(final_size)
    }

    write_thing_to_buffer :: proc(buffer: []byte, ptr: ^$T, head: ^u32) {
        amount := size_of(T)
        mem.copy_non_overlapping(&buffer[head^], ptr, amount)
        head^ += u32(amount)
    }

    write_string_to_buffer :: proc(buffer: []byte, st: string, head: ^u32) {
        amount := u32(len(st))
        write_thing_to_buffer(buffer, &amount, head)
        mem.copy_non_overlapping(&buffer[head^], raw_data(st), int(amount))
        head^ += amount
    }

    write_component_map :: proc(renderer: ^Renderer, string_table: ^StringTable, buffer: []byte, components: map[EntityID]$T, head: ^u32) {
        write_component_string_to_table :: proc(buffer: []byte, string_table: ^StringTable, str: string, head: ^u32) {
            table_entry := string_table_append(string_table, str)
            l := u32(len(table_entry.str))
            write_thing_to_buffer(buffer, &table_entry.offset, head)
            write_thing_to_buffer(buffer, &l, head)
        }

        component_count := u32(len(components))
        write_thing_to_buffer(buffer, &component_count, head)

        for id, &comp in components {
            id := id
            write_thing_to_buffer(buffer, &id, head)
            when T == TriangleMesh {
                write_thing_to_buffer(buffer, &comp.model_matrix, head)
                write_component_string_to_table(buffer, string_table, comp.name, head)
            } else when T == StaticModelInstance {
                write_thing_to_buffer(buffer, &comp.pos_offset, head)
                write_thing_to_buffer(buffer, &comp.flags, head)

                model := get_static_model(renderer, comp.handle)
                write_component_string_to_table(buffer, string_table, model.name, head)

            } else when T == SkinnedModelInstance {
                write_thing_to_buffer(buffer, &comp.pos_offset, head)
                write_thing_to_buffer(buffer, &comp.flags, head)
                write_thing_to_buffer(buffer, &comp.anim_idx, head)

                model := get_skinned_model(renderer, comp.handle)
                write_component_string_to_table(buffer, string_table, model.name, head)

            } else when T == DebugModelInstance {
                write_thing_to_buffer(buffer, &comp.pos_offset, head)
                write_thing_to_buffer(buffer, &comp.color, head)
                write_thing_to_buffer(buffer, &comp.scale, head)

                model := get_static_model(renderer, comp.handle)
                write_component_string_to_table(buffer, string_table, model.name, head)
            } else {
                // Directly serialize the component struct
                write_thing_to_buffer(buffer, &comp, head)
            }
        }
    }

    write_stateless_entities :: proc(buffer: []byte, ids: []EntityID, head: ^u32) {
        size := u32(len(ids))
        write_thing_to_buffer(buffer, &size, head)
        if size == 0 {
            return
        }

        len_bytes := size * size_of(EntityID)
        mem.copy_non_overlapping(&buffer[head^], &ids[0], int(len_bytes))
        head^ += len_bytes
    }

    // Set up intermediate buffer for gathering file data
    string_table := string_table_init(64, temp_allocator)
    total_size := calc_level_file_size(app.game_state, &app.renderer, app.audio_system, &string_table)
    write_head : u32 = 0
    output_buffer := make([dynamic]byte, total_size, temp_allocator)

    // Write magic string
    write_string_to_buffer(output_buffer[:], LEVEL_FILE_MAGIC_STRING, &write_head)

    // Write global offset of string table
    {
        global_offset := total_size - u32(string_table.total_len)
        write_thing_to_buffer(output_buffer[:], &global_offset, &write_head)
    }

    // Write player spawn position
    write_thing_to_buffer(output_buffer[:], &app.game_state.level_start, &write_head)

    // Write bgm filename
    if len(app.audio_system.music_files) > int(app.game_state.bgm_id) {
        bgm := &app.audio_system.music_files[app.game_state.bgm_id]
        write_string_to_buffer(output_buffer[:], bgm.name, &write_head)
    } else {
        write_string_to_buffer(output_buffer[:], "", &write_head)
    }

    // Write directional lights data
    {
        count := app.renderer.directional_light_count
        write_thing_to_buffer(output_buffer[:], &count, &write_head)
        for i in 0..<count {
            light := &app.renderer.directional_lights[i]
            write_thing_to_buffer(output_buffer[:], light, &write_head)
        }
    }

    // Write components to file
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.transforms, &write_head)
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.transform_deltas, &write_head)
    // write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.cameras, &write_head)
    // write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.lookat_controllers, &write_head)
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.enemy_ais, &write_head)
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.hovering_enemies, &write_head)
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.thrown_enemy_ais, &write_head)
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.spherical_bodies, &write_head)
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.triangle_meshes, &write_head)
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.static_models, &write_head)
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.skinned_models, &write_head)
    write_component_map(&app.renderer, &string_table, output_buffer[:], app.game_state.debug_models, &write_head)

    // Write the looping animations and coins lists
    write_stateless_entities(output_buffer[:], app.game_state.looping_animations[:], &write_head)
    write_stateless_entities(output_buffer[:], app.game_state.coins[:], &write_head)

    write_string_table_to_buffer(output_buffer[:], string_table, &write_head)

    // Should have written exactly as many bytes as were allocated
    assert(write_head == total_size)

    // Actually write the buffer to the file
    lvl_file, lvl_err := os.create(path)
    if lvl_err != nil {
        log.errorf("Error opening level file: %v", lvl_err)
    }
    defer os.close(lvl_file)

    _, err := os.write(lvl_file, output_buffer[:])
    if err != nil {
        log.errorf("Error writing level data: %v", err)
    }

    base_path := filepath.stem(path)
    path_clone, p_err := strings.clone(base_path)
    if p_err != nil {
        log.errorf("Error allocating current_level_path string: %v", err)
    }
    app.current_level = path_clone

    log.infof("Finished saving level to \"%v\"", path)
}

lookat_camera_update :: proc(game_state: ^GameState, all_output_verbs: OutputVerbs, viewport_camera_idx: int, dt: f32) {
    HEMISPHERE_START_POS :: hlsl.float4 {1.0, 0.0, 0.0, 0.0}

    id := game_state.viewport_cameras[viewport_camera_idx]
    tform := &game_state.transforms[id]
    camera := &game_state.cameras[id]
    lookat_controller := &game_state.lookat_controllers[id]

    // Make sure we have a valid target
    target, ok := &game_state.transforms[lookat_controller.target]
    for !ok {
        lookat_controller.target = EntityID((u32(lookat_controller.target) + 1) % game_state._next_id)
        target, ok = &game_state.transforms[lookat_controller.target]
    }

    output_verbs := all_output_verbs.recipient_verbs[viewport_camera_idx]
    lookat_controller.distance -= output_verbs.floats[.CameraFollowDistance]
    lookat_controller.distance = math.clamp(lookat_controller.distance, 1.0, 100.0)

    camera_rotation := output_verbs.float2s[.RotateCamera] * dt

    relmotion_coords, ok3 := all_output_verbs.recipient_verbs[VerbRecipient.System].int2s[.MouseMotionRel]
    if ok3 {
        MOUSE_SENSITIVITY :: 0.001

        if .MouseLook in camera.flags {
            camera_rotation += MOUSE_SENSITIVITY * {f32(relmotion_coords.x), f32(relmotion_coords.y)}
        }
    }

    camera.yaw += camera_rotation.x
    camera.pitch += camera_rotation.y
    for camera.yaw < -2.0 * math.PI {
        camera.yaw += 2.0 * math.PI
    }
    for camera.yaw > 2.0 * math.PI {
        camera.yaw -= 2.0 * math.PI
    }
    camera.pitch = clamp(camera.pitch, -math.PI / 2.0 + 0.0001, math.PI / 2.0 - 0.0001)

    // @TODO: Quaternions
    pitchmat := roll_rotation_matrix(-camera.pitch)
    yawmat := yaw_rotation_matrix(-camera.yaw)
    pos_offset := lookat_controller.distance * hlsl.normalize(yawmat * hlsl.normalize(pitchmat * HEMISPHERE_START_POS))

    // Camera follow point chases target
    lookat_controller.current_focal_point = exponential_smoothing(
        lookat_controller.current_focal_point,
        target.position + {0.0, 0.0, lookat_controller.vertical_offset},
        game_state.camera_follow_speed,
        dt
    )

    desired_position := lookat_controller.current_focal_point + pos_offset.xyz
    interval := LineSegment {
        start = lookat_controller.current_focal_point,
        end = desired_position
    }
    s := Sphere {
        position = lookat_controller.current_focal_point,
        radius = 0.1
    }
    hit_t, hit := dynamic_sphere_vs_terrain_t(s, game_state.triangle_meshes, interval)
    if hit {
        desired_position = interval.start + hit_t * (interval.end - interval.start)
    }

    tform.position = desired_position
}

freecam_update :: proc(game_state: ^GameState, all_output_verbs: OutputVerbs, viewport_camera_idx: int, dt: f32) {
    camera_direction: hlsl.float3 = {0.0, 0.0, 0.0}
    camera_speed_mod : f32 = 1.0

    id := game_state.viewport_cameras[viewport_camera_idx]
    tform := &game_state.transforms[id]
    camera := &game_state.cameras[id]

    // Input handling part
    output_verbs := all_output_verbs.recipient_verbs[viewport_camera_idx]
    {
        camera_verbs : []VerbType = {
            .Sprint,
            .Crawl,
            .TranslateFreecamUp,
            .TranslateFreecamDown,
            .TranslateFreecamLeft,
            .TranslateFreecamRight,
            .TranslateFreecamForward,
            .TranslateFreecamBack,
        }
        response_flags : []CameraFlag = {
            .Speed,
            .Slow,
            .MoveUp,
            .MoveDown,
            .MoveLeft,
            .MoveRight,
            .MoveForward,
            .MoveBackward,
        }
        assert(len(camera_verbs) == len(response_flags))

        for verb, i in camera_verbs {
            if verb in output_verbs.bools {
                if output_verbs.bools[verb] {
                    camera.flags += {response_flags[i]}
                } else {
                    camera.flags -= {response_flags[i]}
                }
            }
        }
    }

    camera_rotation: [2]f32 = {0.0, 0.0}
    relmotion_coords, ok3 := all_output_verbs.recipient_verbs[VerbRecipient.System].int2s[.MouseMotionRel]
    if ok3 {
        MOUSE_SENSITIVITY :: 0.001
        if .MouseLook in camera.flags {
            camera_rotation += MOUSE_SENSITIVITY * {f32(relmotion_coords.x), f32(relmotion_coords.y)}
        }
    }

    camera_rotation += output_verbs.float2s[.RotateCamera] * dt
    camera_direction.x += output_verbs.floats[.TranslateFreecamX]

    // Not a sign error. In view-space, -Z is forward
    camera_direction.z -= output_verbs.floats[.TranslateFreecamY]

    camera_speed_mod += game_state.freecam_speed_multiplier * output_verbs.floats[.Sprint]
    camera_speed_mod += game_state.freecam_slow_multiplier * output_verbs.floats[.Crawl]


    CAMERA_SPEED :: 10
    per_frame_speed := CAMERA_SPEED * dt

    if .Speed in camera.flags {
        camera_speed_mod *= game_state.freecam_speed_multiplier
    }
    if .Slow in camera.flags {
        camera_speed_mod *= game_state.freecam_slow_multiplier
    }

    camera.yaw += camera_rotation.x
    camera.pitch += camera_rotation.y
    for camera.yaw < -2.0 * math.PI {
        camera.yaw += 2.0 * math.PI
    }
    for camera.yaw > 2.0 * math.PI {
        camera.yaw -= 2.0 * math.PI
    }

    camera.pitch = clamp(camera.pitch, -math.PI / 2.0, math.PI / 2.0)

    control_flags_dir: hlsl.float3
    if .MoveUp in camera.flags {
        control_flags_dir += {0.0, 1.0, 0.0}
    }
    if .MoveDown in camera.flags {
        control_flags_dir += {0.0, -1.0, 0.0}
    }
    if .MoveLeft in camera.flags {
        control_flags_dir += {-1.0, 0.0, 0.0}
    }
    if .MoveRight in camera.flags {
        control_flags_dir += {1.0, 0.0, 0.0}   
    }
    if .MoveBackward in camera.flags {
        control_flags_dir += {0.0, 0.0, 1.0}
    }
    if .MoveForward in camera.flags {
        control_flags_dir += {0.0, 0.0, -1.0}
    }

    if control_flags_dir != {0.0, 0.0, 0.0} {
        camera_direction += hlsl.normalize(control_flags_dir)
    }

    if camera_direction != {0.0, 0.0, 0.0} {
        camera_direction = hlsl.float3(camera_speed_mod) * hlsl.float3(per_frame_speed) * camera_direction
    }

    // Compute temporary camera matrix for orienting player inputted direction vector
    world_from_view := hlsl.inverse(freecam_view_from_world(tform^, camera^))
    camera_direction4 := hlsl.float4{camera_direction.x, camera_direction.y, camera_direction.z, 0.0}
    tform.position += (world_from_view * camera_direction4).xyz

    // Collision test the camera's bounding sphere against the terrain
    if game_state.freecam_collision {
        scoped_event(&profiler, "Collision with terrain")
        camera_collision_point: hlsl.float3
        closest_dist := math.INF_F32
        for _, &piece in game_state.triangle_meshes {
            candidate := closest_pt_triangles(tform.position, &piece)
            candidate_dist := hlsl.distance(candidate, tform.position)
            if candidate_dist < closest_dist {
                camera_collision_point = candidate
                closest_dist = candidate_dist
            }
        }

        if game_state.freecam_collision {
            dist := hlsl.distance(camera_collision_point, tform.position)
            if dist < CAMERA_COLLISION_RADIUS {
                diff := CAMERA_COLLISION_RADIUS - dist
                tform.position += diff * hlsl.normalize(tform.position - camera_collision_point)
            }
        }
    }
}

camera_gui :: proc(
    game_state: ^GameState,
    input_system: ^InputSystem,
    user_config: ^UserConfiguration,
    close: ^bool
) {
    if imgui.Begin("Camera controls", close) {
        for camera_id, idx in game_state.viewport_cameras {
            imgui.PushIDInt(i32(camera_id))
            defer imgui.PopID()

            tform := &game_state.transforms[camera_id]
            camera := &game_state.cameras[camera_id]
            lookat_controller, is_lookat := &game_state.lookat_controllers[camera_id]

            sb: strings.Builder
            strings.builder_init(&sb, context.temp_allocator)
            fmt.sbprintf(&sb, "Camera id %v", camera_id)
            cs := strings.to_cstring(&sb)
            if imgui.CollapsingHeader(cs) {
                imgui.Text("Position: (%f, %f, %f)", tform.position.x, tform.position.y, tform.position.z)
                imgui.Text("Yaw: %f", camera.yaw)
                imgui.Text("Pitch: %f", camera.pitch)
        
                imgui.SliderFloat("Fast speed", &game_state.freecam_speed_multiplier, 0.0, 100.0)
                imgui.SliderFloat("Slow speed", &game_state.freecam_slow_multiplier, 0.0, 1/5)
                imgui.SliderFloat("Smoothing speed", &game_state.camera_follow_speed, 0.1, 20.0)
                imgui.SameLine()
                if imgui.Button("Reset") {
                    game_state.camera_follow_speed = 6.0
                }
                if imgui.Checkbox("Enable freecam collision", &game_state.freecam_collision) {
                    user_config.flags[.FreecamCollision] = game_state.freecam_collision
                }
        
                freecam := !is_lookat
                if imgui.Checkbox("Freecam", &freecam) {
                    camera.pitch = 0.0
                    camera.yaw = 0.0
        
                    recipient := VerbRecipient(idx)
                    if !freecam {
                        replace_keybindings(input_system, recipient, &game_state.character_key_mappings)
                        game_state.lookat_controllers[camera_id] = LookatController {
                            target = game_state.local_players[0],
                            vertical_offset = DEFAULT_LOOKAT_VERTICAL_OFFSET,
                            distance = DEFAULT_LOOKAT_DISTANCE
                        }
                    } else {
                        replace_keybindings(input_system, recipient, &game_state.freecam_key_mappings)
                        delete_key(&game_state.lookat_controllers, camera_id)
                    }
                }
        
                if is_lookat {
                    imgui.SliderFloat("Camera follow distance", &lookat_controller.distance, 1.0, 20.0)
                    tgt: c.int = c.int(lookat_controller.target)
                    if imgui.SliderInt("Target ID", &tgt, 0, c.int(game_state._next_id - 1)) {
                        lookat_controller.target = EntityID(tgt)
                    }
                    imgui.SliderFloat("Vertical offset", &lookat_controller.vertical_offset, 0.0, 3.0)
                }
        
                imgui.SliderFloat("Camera FOV", &camera.fov_radians, math.PI / 36, math.PI)
                imgui.SameLine()
                imgui.PushIDInt(1)
                if imgui.Button("Reset") {
                    camera.fov_radians = math.PI / 2.0
                }
                imgui.PopID()
            }

        }
    }
    imgui.End()
}